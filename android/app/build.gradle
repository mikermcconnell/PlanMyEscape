import java.util.Properties
import java.io.FileInputStream

apply plugin: 'com.android.application'

android {
    namespace "ca.planmyescape.app"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "ca.planmyescape.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 11
        versionName "2.3.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        release {
            def signingConfigured = false

            def keystorePropertiesFile = rootProject.file('keystore.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                def storeFilePath = keystoreProperties['storeFile']
                if (storeFilePath) {
                    storeFile file(storeFilePath)
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                    signingConfigured = true
                }
            } else {
                def envStoreFile = System.getenv('ANDROID_KEYSTORE_PATH')
                def envStorePassword = System.getenv('ANDROID_KEYSTORE_STORE_PASSWORD')
                def envAlias = System.getenv('ANDROID_KEYSTORE_KEY_ALIAS')
                def envKeyPassword = System.getenv('ANDROID_KEYSTORE_KEY_PASSWORD')

                if (envStoreFile && envAlias) {
                    storeFile file(envStoreFile)
                    storePassword envStorePassword
                    keyAlias envAlias
                    keyPassword envKeyPassword
                    signingConfigured = true
                }
            }

            if (!signingConfigured) {
                println 'Warning: Release signing is not configured. Provide keystore.properties or environment variables before building a release.'
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}

